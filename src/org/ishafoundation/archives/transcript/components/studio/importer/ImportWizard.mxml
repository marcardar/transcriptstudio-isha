<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" width="470" height="600" horizontalAlign="center" title="{getTitle(viewstack.selectedChild)}" creationComplete="init()" xmlns:metadata="org.ishafoundation.archives.transcript.components.studio.metadata.*" xmlns:importer="org.ishafoundation.archives.transcript.components.studio.importer.*">
	<mx:Script>
	<![CDATA[
		import mx.managers.PopUpManager;
		import org.ishafoundation.archives.transcript.components.studio.metadata.EventPropertiesPane;
		import mx.managers.CursorManager;
		import mx.controls.Alert;
		import org.ishafoundation.archives.transcript.components.studio.metadata.EventPropertiesCreateDialog;
		import org.ishafoundation.archives.transcript.model.SessionProperties;
		import org.ishafoundation.archives.transcript.model.EventProperties;
		import org.ishafoundation.archives.transcript.importer.MSWordImporter;
		import org.ishafoundation.archives.transcript.model.ReferenceManager;
		import org.ishafoundation.archives.transcript.db.XQueryExecutor;
		import org.ishafoundation.archives.transcript.db.DatabaseManager;
		import mx.core.Container;
		import mx.events.PropertyChangeEvent;
		import mx.binding.utils.ChangeWatcher;
		
		public static const OK_CLICKED:String = "OK_CLICKED";
		
		private var _databaseMgr:DatabaseManager;
		
		[Bindable]
		public var referenceMgr:ReferenceManager;
		
		public var importer:MSWordImporter;
		
		public var sessionXML:XML;
		private var importedEventProps:EventProperties;
		private var importedSessionProps:SessionProperties;
		public var eventProps:EventProperties;
	
		private function init():void {
			for each (var child:Container in viewstack.getChildren()) {
				if (ChangeWatcher.canWatch(child, "ready")) {
					ChangeWatcher.watch(child, "ready", function(evt:PropertyChangeEvent):void {
						buttonBox.executeBindings(true);
					});
				}
			}
			var thisPanel:ImportWizard = this;
			eventSelectorPane.createEventButton.addEventListener(MouseEvent.CLICK, function(evt:MouseEvent):void {
				var popup:EventPropertiesCreateDialog = EventPropertiesCreateDialog.display(thisPanel, databaseMgr, referenceMgr, importedEventProps.copy());
				popup.eventPropertiesPane.eventTypeComboBox.enabled = false;
				popup.addEventListener(EventPropertiesCreateDialog.EVENT_CREATED, function(event:Event):void {
					eventProps = popup.eventPropertiesPane.eventProps;
					sessionPropertiesPane.eventProps = eventProps;
					sessionPropertiesPane.sessionProps = importedSessionProps;
					sessionPropertiesPane.sessionProps.eventId = eventProps.id;
					status = "Successfully created event: " + eventProps.id;
					Alert.show("Event ID: " + eventProps.id, "Successfully created event");
					nextInternal();
				});
			});
		}
		
		private function getTitle(vsChild:Container):String {
			if (vsChild == importFilesPane) {
				title = "Select file(s) to import...";
			}
			else if (vsChild == importFilesPreviewPane) {
				title = "Preview import files...";
			}
			else if (vsChild == eventSelectorPane) {
				title = "Select existing event...";
			}
			else if (vsChild == sessionPropertiesPane) {
				title = "Session properties...";
			}
			else {
				title = "Unknown pane";
			}
			return title;
		}
		
		[Bindable]
		public function get databaseMgr():DatabaseManager {
			return _databaseMgr;
		}

		public function set databaseMgr(databaseMgr:DatabaseManager):void {
			_databaseMgr = databaseMgr;
			setImportListDataProvider(databaseMgr);
		}
		
		private function backClicked(evt:MouseEvent):void {
			viewstack.selectedIndex--;
			/*if (viewstack.selectedChild == eventSelectorPane) {
				updateEventsList();
			}*/
		}

		private function nextClicked(evt:MouseEvent):void {
			// postprocess pane
			if (viewstack.selectedChild == importFilesPane) {
				enabled = false;
				var selectedPaths:Array = [];
				for each (var name:String in importList.selectedItems.reverse()) {
					selectedPaths.push(name);
				}
				selectedPaths.sort();
				CursorManager.setBusyCursor();
				importer.importAudioTranscripts(selectedPaths, function(audioTranscripts:Array):void {
					CursorManager.removeBusyCursor();
					importFilesPreviewPane.audioTranscripts = audioTranscripts;
					enabled = true;
					nextInternal();
				}, function (msg:String):void {
					CursorManager.removeBusyCursor();
					Alert.show("Import failed: " + msg);
					enabled = true;
					return;
				});
			}
			else if (viewstack.selectedChild == importFilesPreviewPane) {
				var eventXML:XML = MSWordImporter.createEventElement(importFilesPreviewPane.audioTranscripts);
				sessionXML = MSWordImporter.createSessionElement(importFilesPreviewPane.audioTranscripts);
				importedEventProps = new EventProperties(eventXML);
				importedSessionProps = new SessionProperties(sessionXML);
				if (importedEventProps.startAt == null) {
					importedEventProps.startAt = importedSessionProps.startAt;
				}
				if (importedEventProps.endAt == null) {
					importedEventProps.endAt = importedEventProps.startAt;
				}
				nextInternal();
			}
			else if (viewstack.selectedChild == eventSelectorPane) {
				eventProps = eventSelectorPane.selectedEventProps;
				if (eventProps == null) {
					throw new Error("Tried to progress without selecting event");
				}
				nextInternal();
			}
		}
			
		private function nextInternal():void {
			viewstack.selectedIndex++;
			// preprocess pane
			if (viewstack.selectedChild == importFilesPane) {
			}
			else if (viewstack.selectedChild == importFilesPreviewPane) {
				
			}
			else if (viewstack.selectedChild == eventSelectorPane) {
				eventProps = null;
				eventSelectorPane.eventTypeComboBox.selectedItem = importedEventProps.type;
				eventSelectorPane.eventTypeComboBox.enabled = false;
				if (importedEventProps.startAt != null) {
					eventSelectorPane.yearComboBox.selectedItem = importedEventProps.startAt.fullYear.toString();
				}
				eventSelectorPane.searchClicked();
			}
			else if (viewstack.selectedChild == sessionPropertiesPane) {
				importedSessionProps.eventId = eventProps.id;
				sessionPropertiesPane.eventProps = eventProps;
				sessionPropertiesPane.sessionProps = importedSessionProps;
			}
		}
		
		private function finishClicked(event:Event):void {
			// now we might have the session date so use that to prefix the media UUIDs
			var mediaIdPrefix:String;
			if (sessionPropertiesPane.sessionProps.startAt == null) {
				mediaIdPrefix = "00000000";
			}
			else {
				var startAtDate:Date = sessionPropertiesPane.sessionProps.startAt;
				mediaIdPrefix = startAtDate.fullYear + EventPropertiesPane.fixWidth(startAtDate.month + 1, 2) + EventPropertiesPane.fixWidth(startAtDate.date, 2);
			}
			// use the digits from the sourceId in the new mediaId
			mediaIdPrefix += "-" + eventProps.type;
			mediaIdPrefix += "1"; // TODO: maybe sometimes this should really be "2" or even "3"
			mediaIdPrefix += "-a1";
			for each (var mediaElement:XML in sessionXML.devices.device.media) {
				var sourceId:String = mediaElement.@id.toString();
				var mediaIdSuffix:String = sourceId.replace(/[a-z]/ig, "");
				while (mediaIdSuffix.length < 6) {
					mediaIdSuffix = "0" + mediaIdSuffix;
				}
				mediaElement.@id = mediaIdPrefix + "-" + mediaIdSuffix;
			}
			dispatchEvent(new Event(OK_CLICKED));
			closeMe();
		}
		
		private function closeMe():void {
			PopUpManager.removePopUp(this);
		}
		
		private function isBackEnabled():Boolean {
			return viewstack != null && viewstack.selectedIndex > 0 && viewstack.selectedChild != sessionPropertiesPane;
		}
		
		private function isNextEnabled():Boolean {
			if (viewstack == null || viewstack.selectedIndex == viewstack.numChildren - 1) {
				// this is the last stack
				return false;
			}
			if (viewstack.selectedChild == importFilesPane) {
				return importList != null && importList.selectedIndices.length > 0;
			}
			else if (viewstack.selectedChild == importFilesPreviewPane) {
				return true;
			}
			else if (viewstack.selectedChild == eventSelectorPane) {
				return eventSelectorPane.ready;
			}
			else {
				return false;			
			}
		}
		
		private function isFinishEnabled():Boolean {
			if (viewstack.selectedIndex == viewstack.numChildren - 1) {
				// this is the last child
				return sessionPropertiesPane.ready;
			}
			else {
				return false;
			}
		}
		
		/*private static function getImportFiles(fileSystem:DbFileSystem):Array {
			var result:Array = fileSystem.getCollection(DatabaseConstants.IMPORT_COLLECTION_PATH).otherFiles;
			result.sort();
			return result;			
		}*/
		
		private function setImportListDataProvider(xqueryExecutor:XQueryExecutor):void {
			xqueryExecutor.query("import module namespace ts4isha='http://ishafoundation.org/ts4isha/xquery' at 'java:org.ishafoundation.ts4isha.xquery.modules.TranscriptStudioModule';ts4isha:import-file-name-list()", [], function(xml:XML):void {
				var names:Array = [];
				var nameAttrs:XMLList = xml.*.*.@name;
				for each (var name:String in nameAttrs) {
					names.push(name);
				}
				names.sort();
				importList.dataProvider = names;
			}, function (msg:String):void {
				Alert.show(msg, "Could not read import files listing");
			});
		}
		
	]]>
	</mx:Script>
	
	<mx:ViewStack id="viewstack" width="100%" height="100%" change="buttonBox.executeBindings(true)" creationPolicy="all">
		<mx:VBox id="importFilesPane" label="Import Session Files" width="100%" height="100%">
			<mx:List id="importList" width="100%" height="100%" allowMultipleSelection="true" change="buttonBox.executeBindings(true)"/>
		</mx:VBox>
		<importer:ImportFilesPreviewPane id="importFilesPreviewPane" width="100%" height="100%"/>
		<metadata:EventSelectorPane id="eventSelectorPane" width="100%" height="100%" xqueryExecutor="{databaseMgr}" referenceMgr="{referenceMgr}"/>
		<metadata:SessionPropertiesPane id="sessionPropertiesPane" label="Session Properties" width="100%" height="100%" referenceMgr="{referenceMgr}"/>
	</mx:ViewStack>
	<mx:HBox id="buttonBox">
		<mx:Button label="&lt; Back" enabled="{isBackEnabled()}" click="backClicked(event)"/>
		<mx:Button id="nextButton" label="Next &gt;" enabled="{isNextEnabled()}" click="nextClicked(event)"/>
		<mx:Button label="Finish" enabled="{isFinishEnabled()}" click="finishClicked(event)" />
		<mx:Button label="Cancel" click="closeMe()"/>
	</mx:HBox>
	
</mx:TitleWindow>
