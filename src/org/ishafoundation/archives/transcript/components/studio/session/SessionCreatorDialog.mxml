<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" width="470" height="600" horizontalAlign="center" title="Create Session" xmlns:session="org.ishafoundation.archives.transcript.components.studio.session.*">
	<mx:Script>
	<![CDATA[
		import org.ishafoundation.archives.transcript.db.DatabaseManagerUtils;
		import name.carter.mark.flex.util.XMLUtils;
		import org.ishafoundation.archives.transcript.model.Session;
		import org.ishafoundation.archives.transcript.model.SessionMetadata;
		import org.ishafoundation.archives.transcript.db.XMLStorer;
		import org.ishafoundation.archives.transcript.db.XQueryExecutor;
		import mx.events.FlexEvent;
		import org.ishafoundation.archives.transcript.model.ReferenceManager;
		import org.ishafoundation.archives.transcript.model.EventMetadata;
		import org.ishafoundation.archives.transcript.db.DatabaseManager;
		import mx.controls.Alert;
		import mx.managers.PopUpManager;
		
		public static const SESSION_EDITED:String = "SESSION_EDITED";
		
		private var xqueryExecutor:XQueryExecutor;
		
		public static function display(parent:DisplayObject, xqueryExecutor:XQueryExecutor, referenceMgr:ReferenceManager, eventMetadata:EventMetadata, sessionMetadataElement:XML):SessionCreatorDialog {
			var sessionMetadata:SessionMetadata = new SessionMetadata(sessionMetadataElement, eventMetadata.id);
			var result:SessionCreatorDialog = new SessionCreatorDialog();
			result.xqueryExecutor = xqueryExecutor;
			PopUpManager.addPopUp(result, parent, true);
			result.sessionMetadataPane.addEventListener(FlexEvent.CREATION_COMPLETE, function(evt:FlexEvent):void {
				result.sessionMetadataPane.referenceMgr = referenceMgr;
				result.sessionMetadataPane.selectableStartAtRange = eventMetadata.dateRange;
				if (sessionMetadata.startAt == null) {
					sessionMetadata.startAt = eventMetadata.startAt;
				}
				result.sessionMetadataPane.sessionMetadata = sessionMetadata;
			}, true);
			return result;
		}
	
		public function get sessionMetadata():SessionMetadata {
			return sessionMetadataPane.sessionMetadata;
		}
		
		private function okClicked():void {
			var metadataElement:XML = sessionMetadataPane.sessionMetadata.metadataElement;
			DatabaseManagerUtils.createSession(sessionMetadata.eventId, metadataElement, xqueryExecutor, function(sessionXML:XML):void {
				var metadataXML:XML = sessionXML.metadata[0];
				sessionMetadataPane.sessionMetadata = new SessionMetadata(metadataElement, sessionMetadata.eventId, sessionXML.@id);
				closeMe();
				dispatchEvent(new Event(SESSION_EDITED));
			}, function (msg:String):void {
				Alert.show(msg, "Could not store session");
			});
		}
		
		public function closeMe():void {
			PopUpManager.removePopUp(this);
		}
	]]>
	</mx:Script>
	
	<session:SessionMetadataPane id="sessionMetadataPane" width="100%" height="100%"/>
	<mx:HBox id="buttonBox">
		<mx:Button id="okButton" label="OK" enabled="{sessionMetadataPane.ready}" click="okClicked()"/>
		<mx:Button label="Cancel" click="closeMe()"/>
	</mx:HBox>
	
</mx:TitleWindow>
