<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" minWidth="320" layout="vertical" horizontalAlign="center" title="Media Player" creationComplete="init()" xmlns:generic="org.ishafoundation.archives.transcript.components.generic.*" xmlns:display="flash.display.*">
	<mx:Script>
    <![CDATA[
    	import org.ishafoundation.archives.transcript.media.MediaConstants;
    	import org.ishafoundation.archives.transcript.media.MediaConnection;
    	import mx.events.PropertyChangeEvent;
    	import mx.binding.utils.ChangeWatcher;
    	import mx.events.SliderEventClickTarget;
    	import mx.events.SliderEvent;
    	import mx.binding.utils.BindingUtils;
    	import org.ishafoundation.archives.transcript.components.generic.PleaseWaitPopUp;
    	import org.ishafoundation.archives.transcript.components.generic.MediaServerPopUp;
    	import flash.sampler.getSetterInvocationCount;
    	import org.ishafoundation.archives.transcript.model.Session;
    	import name.carter.mark.flex.util.XMLUtils;
    	import mx.controls.Alert;
    	import mx.collections.ArrayCollection;
    	import org.ishafoundation.archives.transcript.db.DatabaseConstants;
    	import flash.utils.getTimer;
    	import flash.net.sendToURL;
    	import mx.managers.PopUpManager;
    	import mx.core.UIComponent;
		import mx.events.ResizeEvent;

		[Bindable]
		public var devicesElement:XML;
		
		private static const dummyDevicesElement:XML =
			<devices>
				<device id="a1" name="Mixer"> <!-- n124.mp3 -->
					<clip id="c124" audioQuality="excellent">
						<syncPoint id="sp1" timecode="15"/>
						<syncPoint id="sp2" timecode="17"/>
					</clip>
					<clip id="c125" audioQuality="excellent">
					</clip>
				</device>	
				<device id="v1" name="Main Camera"> <!-- n866.mp4 -->
					<clip id="c866" videoQuality="good" audioQuality="good">
					</clip>
				</device>
			</devices>;
		
		private var video:Video = new Video();
		[Bindable]
		private var mediaServer:MediaConnection = new MediaConnection(); 
		
		[Bindable]
		public var isPlaying:Boolean = false;
		private var monitorTimer:Timer = new Timer(1000);
		
		private function init():void {
			videoBox.addChild(video);
			connectToMediaServer();
			monitorTimer.addEventListener(TimerEvent.TIMER, function(evt:TimerEvent):void {
				if (!isPlaying) {
					return;
				}
				var newTime:Number = mediaServer.mediaStream.time;
				if (newTime == this.seekFromTime) {
					trace("Looks like seek is still in progress... (current time=" + newTime + ")");
				}
				else {
					trace("Updating slider: " + newTime);
					slider.value = newTime;
				}
			});
			ChangeWatcher.watch(this, "isPlaying", function(evt:PropertyChangeEvent):void {
				if (evt.newValue) {
					monitorTimer.start();
				}
				else {
					monitorTimer.stop();
				}
			});
			// some dummy data
			/*var my_nc:NetConnection = new NetConnection();
			my_nc.connect(null);

			var my_ns:NetStream = new NetStream(my_nc);
			my_video.attachVideo(my_ns);*/

			this.devicesElement = dummyDevicesElement;
		}
		
		private static function getDeviceIds(devicesElement:XML):Array {
			var result:Array = [];
			for each (var deviceElement:XML in devicesElement.device) {
				result.push(deviceElement.@id.toString());
			}
			return result;
		}
		
		private function getClipIds(deviceId:String):Array {
			if (deviceId == null) {
				return [];
			}
			var result:Array = [];
			var clipElements:XMLList = this.devicesElement.device.(@id == deviceId).clip;
			for each (var clipElement:XML in clipElements) {
				result.push(clipElement.@id.toString())
			}
			return result;
		}
		
		private function deviceLabelFunc(deviceId:String):String {
			if (deviceId == null || deviceId.length == 0) {
				return "<Select Device>";
			}
			var deviceElement:XML = devicesElement.device.(@id == deviceId)[0];
			return deviceElement.@name;
		} 
		
		private function connectToMediaServer():void {
			var waitPopUp:PleaseWaitPopUp = PleaseWaitPopUp.display(this, "Trying to connect...");
			var thisHandle:MediaPlayerDialog = this;
			this.mediaServer.connect(function():void {
				waitPopUp.closePopUp();
				Alert.show("Connected");
			}, function(msg:String):void {
				mediaServer.close();
				waitPopUp.closePopUp();
				if (msg == "NetConnection.Connect.Failed") {
					var popUp:MediaServerPopUp = PopUpManager.createPopUp(thisHandle, MediaServerPopUp, true) as MediaServerPopUp;
						PopUpManager.centerPopUp(popUp);
						popUp.okButton.addEventListener(MouseEvent.CLICK, function(evt:MouseEvent):void {
							MediaConstants.MEDIA_SERVER_URL = popUp.urlTextInput.text;
							PopUpManager.removePopUp(popUp);
							connectToMediaServer();
						});
				}
				else {
					Alert.show(msg, "Connect failed");
				}
			});			
		}
		
		private static function getClipItem(clipElement:XML):Object {
			//var sessionElement:XML = XMLUtils.getAncestorOrSelfElement(clipElement, "session");
			//var sessionId:String = sessionElement.@id;
			var sessionWideClipId:String = getSessionWideClipId(clipElement);
			var extension:String = isVideo(clipElement) ? "flv" : "mp3";
			var filename:String = sessionWideClipId + "." + extension;
			var result:Object = {filename:filename, id:sessionWideClipId};
			return result;			
		}
		
		private static function isVideo(clipOrDeviceElement:XML):Boolean {
			var deviceElement:XML = XMLUtils.getAncestorOrSelfElement(clipOrDeviceElement, "device");
			return deviceElement.@id.toString().indexOf("v") == 0;
		}
		
		/**
		 *  sessionWideClipId - eg "v1c001"
		 */
		public function play(sessionWideClipId:String, start:int = -1, end:int = -1):void {
			if (sessionWideClipId == null) {
				return;
			}
			var clipElement:XML = getClipElement(sessionWideClipId);
			var clipURL:String = sessionWideClipId + "." + (sessionWideClipId.indexOf("v") == 0 ? "flv" : "mp3");
			var waitPopUp:PleaseWaitPopUp = PleaseWaitPopUp.display(this, "Preparing to stream...");
			this.slider.value = 0;
			this.mediaServer.play(clipURL, video, function(msg:String):void {
				trace("Status msg: " + msg);
				if (msg.indexOf("NetStream.Play.Complete") >= 0) {					
					isPlaying = false;
					slider.value = 0;
					seekFromTime = -1;
					mediaServer.mediaStream.close();
					return;
				}
				// update videoWidth
				if (video.videoWidth > 0) {
					videoBox.width = video.videoWidth;
				}
				if (video.videoHeight > 0) {
					videoBox.height = video.videoHeight;
				}
				waitPopUp.closePopUp();
			}, function(msg:String):void {
				isPlaying = false;
				waitPopUp.closePopUp();
				Alert.show(msg);
			}, start, end);
			this.seekFromTime = -1;
			this.isPlaying = true;
			//var mediaURL:String = "rtmp://localhost/vod/" + mediaItem.type + ":" + mediaItem.filenameWithoutExtension;
			//videoDisplay.playheadTime = 0;
			//videoDisplay.source = mediaURL;
		}
		
		private function getClipElement(sessionWideClipId:String):XML {
			if (sessionWideClipId == null) {
				throw new Error("Passed a null sessionWideClipId");
			}
			if (sessionWideClipId.length < 6) { // TODO - use proper regexp?
				throw new Error("Invalid clipId (should be like 'v1c123' or 'a1c234'): " + sessionWideClipId);
			}
			var deviceId:String = sessionWideClipId.substring(0, 2);
			var localClipId:String = sessionWideClipId.substring(2);
			var deviceElement:XML = devicesElement.device.(@id == deviceId)[0]; 
			var result:XML = deviceElement.clip.(@id == localClipId)[0];
			return result;			
		}
		
		private static function getSessionWideClipId(clipElement:XML):String {
			var deviceElement:XML = clipElement.parent();
			return deviceElement.@id.toString() + clipElement.@id.toString();
		}
		
		private function getSelectedSessionWideClipId():String {
			var deviceId:String = deviceComboBox.selectedItem as String;
			if (deviceId == null) {
				return null;
			}
			var clipId:String = clipComboBox.selectedItem as String;
			if (clipId == null) {
				return null;
			}
			return deviceId + clipId;
		}
		
		private function deviceChangeHandler():void {
			trace("Changing device");
			var clipIds:Array = getClipIds(deviceComboBox.selectedItem as String);
			clipComboBox.dataProvider = clipIds;
			clipComboBox.enabled = clipIds.length > 1;
			var sessionWideClipId:String = getSelectedSessionWideClipId();
			if (sessionWideClipId == null) {
				videoBox.visible = false;				
				videoBox.includeInLayout = false;
			}
			else {
				if (sessionWideClipId.indexOf("v") == 0) {
					videoBox.visible = true;
				}
				else {
					videoBox.visible = false;
				}
				videoBox.includeInLayout = videoBox.visible;
				// check the clip id for the old device is not still there
				play(sessionWideClipId);
			}
		}
		
		private function clipChangeHandler():void {
			trace("Changing clip");
			var sessionWideClipId:String = getSelectedSessionWideClipId();
			play(sessionWideClipId);
		}
		
    	public static function display(parent:DisplayObject):MediaPlayerDialog {
    		var result:MediaPlayerDialog = new MediaPlayerDialog();
			PopUpManager.addPopUp(result, parent, true);
			return result;
    	}
    
		private function closeMe():void {
        	PopUpManager.removePopUp(this);
        	this.isPlaying = false;
        	this.mediaServer.close();
		}
		
		private function skipClicked():void {
			//playerObject.view.sendEvent(ViewEvent.SEEK, 5);
		}
		
		/**
		 * When seeking, the old time will continue to be reported by the stream until the seek has reached its destination.
		 * If we don't handle this properly then you get an ugly situation where you drag the slider, release the
		 * mouse button and then the slider jumps back to its old place before correcting itself
		 */
		[Bindable]
		private var seekFromTime:Number = -1;
		
		private function playPauseClickHandler():void {
			if (this.isPlaying) {
				mediaServer.mediaStream.pause();
			}
			else if (mediaServer.mediaStream.isOpen()) {
				mediaServer.mediaStream.resume();
			}
			else {
				mediaServer.mediaStream.play(video);
			}
			this.isPlaying = !this.isPlaying;
		}
		
		private function sliderChange(evt:SliderEvent):void {
			if (evt.clickTarget != null) {
				// workaround for a bug:
				if (evt.value == 0 && this.seekFromTime < 0) {
					return;
				}
				if (!mediaServer.mediaStream.isOpen()) {
					return;
				}
				// change was not caused by the media itself
				trace("Slider change: " + evt.value + ", " + evt.clickTarget);
				this.seekFromTime = mediaServer.mediaStream.time;
				this.slider.value = evt.value;
				mediaServer.mediaStream.seek(Math.floor(evt.value));
			}
		}
		
    ]]>
	</mx:Script>
	<mx:Grid width="100%" horizontalGap="20" paddingLeft="20" paddingRight="20" verticalGap="0">
		<mx:GridRow width="100%" height="100%">
			<mx:GridItem width="50%">
				<mx:Label width="100%" text="Device"/>
			</mx:GridItem>
			<mx:GridItem width="50%">
				<mx:Label width="100%" text="Media Clip"/>
			</mx:GridItem>
		</mx:GridRow>
		<mx:GridRow width="100%" height="100%">
			<mx:GridItem width="50%" height="100%">
				<mx:ComboBox id="deviceComboBox" prompt="Select Device..." labelFunction="{deviceLabelFunc}" dataProvider="{getDeviceIds(this.devicesElement)}" change="deviceChangeHandler()"/>
			</mx:GridItem>
			<mx:GridItem width="50%" height="100%">
				<mx:ComboBox minWidth="150" id="clipComboBox" enabled="false" change="clipChangeHandler()"/>
			</mx:GridItem>
		</mx:GridRow>
	</mx:Grid>

	<mx:Image id="videoBox" minWidth="320" minHeight="250" scaleContent="false" autoLoad="true"  width="100%" height="100%" horizontalAlign="center" verticalAlign="middle"/> 
	<mx:HBox width="100%" verticalAlign="middle">
		<mx:Button label="{isPlaying ? 'Pause' : 'Play'}" id="playPauseButton" enabled="{mediaServer.mediaStream != null}" click="playPauseClickHandler()" width="70"/>
		<mx:HSlider width="100%" id="slider" liveDragging="true" slideDuration="100" showTrackHighlight="true" enabled="{mediaServer.mediaStream != null &amp;&amp; mediaServer.mediaStream.duration &gt; 0}" minimum="0" maximum="{mediaServer.mediaStream.duration}" change="sliderChange(event)"/>
	</mx:HBox>
	<!--mx:Text text="Time tracker" id="timelabel" /-->

	<mx:HBox>
		<!--mx:Button label="Skip" id="skipButton" click="skipClicked()"/-->
		<mx:Button label="OK" id="okButton" click="closeMe()"/>		
	</mx:HBox>
</mx:TitleWindow>
