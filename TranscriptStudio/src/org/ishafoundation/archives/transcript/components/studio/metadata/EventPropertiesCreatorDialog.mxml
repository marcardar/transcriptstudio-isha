<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" width="470" height="600" horizontalAlign="center" title="Create Event" xmlns:metadata="org.ishafoundation.archives.transcript.components.studio.metadata.*">
	<mx:Script>
	<![CDATA[
		import org.ishafoundation.archives.transcript.db.DatabaseManagerUtils;
		import org.ishafoundation.archives.transcript.db.XMLStorer;
		import org.ishafoundation.archives.transcript.db.XQueryExecutor;
		import mx.events.FlexEvent;
		import org.ishafoundation.archives.transcript.model.ReferenceManager;
		import org.ishafoundation.archives.transcript.model.EventProperties;
		import org.ishafoundation.archives.transcript.db.DatabaseManager;
		import mx.controls.Alert;
		import mx.managers.PopUpManager;
		
		public static const EVENT_EDITED:String = "EVENT_EDITED";
		
		private var xqueryExecutor:XQueryExecutor;
		
		public static function display(parent:DisplayObject, xqueryExecutor:XQueryExecutor, referenceMgr:ReferenceManager, eventProps:EventProperties):EventPropertiesCreatorDialog {
			var result:EventPropertiesCreatorDialog = new EventPropertiesCreatorDialog();
			result.xqueryExecutor = xqueryExecutor;
			PopUpManager.addPopUp(result, parent, true);
			result.eventPropertiesPane.addEventListener(FlexEvent.CREATION_COMPLETE, function(evt:FlexEvent):void {
				result.eventPropertiesPane.referenceMgr = referenceMgr;
				result.eventPropertiesPane.callLater(function():void {
					if (eventProps == null) {
						result.eventPropertiesPane.eventProps = new EventProperties(<metadata/>, 'a');
					}
					else {
						result.eventPropertiesPane.eventProps = eventProps;
					}
				});
			}, true);
			return result;
		}
	
		public function get eventProps():EventProperties {
			return eventPropertiesPane.eventProps;
		}
		
		private function okClicked():void {
			DatabaseManagerUtils.createEvent(eventProps.type, eventProps.eventMetadataElement, xqueryExecutor, function(eventXML:XML):void {
				eventProps.id = eventXML.@id;
				closeMe();
				dispatchEvent(new Event(EVENT_EDITED));
			}, function (msg:String):void {
				Alert.show(msg, "Could not store event file");
			});
		}
		
		public function closeMe():void {
			PopUpManager.removePopUp(this);
		}
	]]>
	</mx:Script>
	
	<metadata:EventPropertiesPane id="eventPropertiesPane" width="100%" height="100%"/>
	<mx:HBox id="buttonBox">
		<mx:Button id="okButton" label="OK" enabled="{eventPropertiesPane.ready}" click="okClicked()"/>
		<mx:Button label="Cancel" click="closeMe()"/>
	</mx:HBox>
	
</mx:TitleWindow>
