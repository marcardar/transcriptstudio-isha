<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" width="470" height="600" horizontalAlign="center" title="Create Event" xmlns:event="org.ishafoundation.archives.transcript.components.studio.event.*">
	<mx:Script>
	<![CDATA[
		import org.ishafoundation.archives.transcript.model.EventMetadata;
		import org.ishafoundation.archives.transcript.db.DatabaseManagerUtils;
		import org.ishafoundation.archives.transcript.db.XMLStorer;
		import org.ishafoundation.archives.transcript.db.XQueryExecutor;
		import mx.events.FlexEvent;
		import org.ishafoundation.archives.transcript.model.ReferenceManager;
		import org.ishafoundation.archives.transcript.db.DatabaseManager;
		import mx.controls.Alert;
		import mx.managers.PopUpManager;
		
		public static const EVENT_EDITED:String = "EVENT_EDITED";
		
		private var xqueryExecutor:XQueryExecutor;
		
		public static function display(parent:DisplayObject, xqueryExecutor:XQueryExecutor, referenceMgr:ReferenceManager, eventMetadata:EventMetadata):EventCreatorDialog {
			var result:EventCreatorDialog = new EventCreatorDialog();
			result.xqueryExecutor = xqueryExecutor;
			PopUpManager.addPopUp(result, parent, true);
			result.eventMetadataPane.addEventListener(FlexEvent.CREATION_COMPLETE, function(evt:FlexEvent):void {
				result.eventMetadataPane.referenceMgr = referenceMgr;
				result.eventMetadataPane.callLater(function():void {
					if (eventMetadata == null) {
						result.eventMetadataPane.eventMetadata = new EventMetadata(<metadata/>, 'a');
					}
					else {
						result.eventMetadataPane.eventMetadata = eventMetadata;
					}
				});
			}, true);
			return result;
		}
	
		public function get eventMetadata():EventMetadata {
			return eventMetadataPane.eventMetadata;
		}
		
		private function okClicked():void {
			DatabaseManagerUtils.createEvent(eventMetadata.type, eventMetadata.eventMetadataElement, xqueryExecutor, function(eventXML:XML):void {
				eventMetadata.id = eventXML.@id;
				closeMe();
				dispatchEvent(new Event(EVENT_EDITED));
			}, function (msg:String):void {
				Alert.show(msg, "Could not store event file");
			});
		}
		
		public function closeMe():void {
			PopUpManager.removePopUp(this);
		}
	]]>
	</mx:Script>
	
	<event:EventMetadataPane id="eventMetadataPane" width="100%" height="100%"/>
	<mx:HBox id="buttonBox">
		<mx:Button id="okButton" label="OK" enabled="{eventMetadataPane.ready}" click="okClicked()"/>
		<mx:Button label="Cancel" click="closeMe()"/>
	</mx:HBox>
	
</mx:TitleWindow>
