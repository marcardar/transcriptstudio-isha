<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" width="470" height="600" horizontalAlign="center" title="Select Event" creationComplete="init()" xmlns:metadata="org.ishafoundation.archives.transcript.components.studio.metadata.*">
	<mx:Script>
	<![CDATA[
		import mx.managers.PopUpManager;
		import org.ishafoundation.archives.transcript.model.EventProperties;
		import mx.events.FlexEvent;
		import org.ishafoundation.archives.transcript.model.ReferenceManager;
		import org.ishafoundation.archives.transcript.db.XQueryExecutor;

		public static const OPEN_SESSION_REQUEST:String = "OPEN_SESSION_REQUEST";		

		/* this should really go in the dispatched event */
		public var selectedSessionId:String;

		public static function display(parent:DisplayObject, xqueryExecutor:XQueryExecutor, referenceMgr:ReferenceManager):OpenDialog {
			var result:OpenDialog = new OpenDialog();
			PopUpManager.addPopUp(result, parent, true);
			result.eventSelectorPane.createEventButton.visible = false;
			result.eventSelectorPane.createEventButton.includeInLayout = false;
			result.eventSelectorPane.addEventListener(FlexEvent.CREATION_COMPLETE, function(evt:FlexEvent):void {
				result.eventSelectorPane.xqueryExecutor = xqueryExecutor;
				result.eventSelectorPane.referenceMgr = referenceMgr;
			}, true);
			return result;
		}
	
		private function init():void {
		}
		
		private function okClicked(evt:MouseEvent):void {
			SessionSelectorDialog.displayIfNecessary(selectedEventProps, this, eventSelectorPane.xqueryExecutor, function(sessionId:String):void {
				selectedSessionId = sessionId;
				dispatchEvent(new Event(OPEN_SESSION_REQUEST, true));
				closeMe();
			});
		}
		
		private function get selectedEventProps():EventProperties {
			return eventSelectorPane.eventsList.selectedItem as EventProperties;
		}
		
		private function listDoubleClickHandler(evt:MouseEvent):void {
			if (selectedEventProps == null) {
				return;
			}
			okClicked(evt);
		}
		
		private function closeMe():void {
			PopUpManager.removePopUp(this);
		}
	]]>
	</mx:Script>
	
	<metadata:EventSelectorPane id="eventSelectorPane" label="Select Event" width="100%" height="100%" doubleClickEnabled="true" doubleClick="{listDoubleClickHandler(event)}"/>
	<mx:HBox id="buttonBox">
		<mx:Button label="OK" enabled="{eventSelectorPane.eventsList.selectedItem != null}" click="okClicked(event)" />
		<mx:Button label="Cancel" click="closeMe()"/>
	</mx:HBox>
	
</mx:TitleWindow>
