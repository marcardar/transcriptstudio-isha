<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="init()" xmlns:generic="org.ishafoundation.archives.transcript.components.generic.*">
	<mx:Script>
	<![CDATA[
		import org.ishafoundation.archives.transcript.components.generic.MessagePopUp;
		import name.carter.mark.flex.util.Utils;
		import mx.controls.Alert;
		import org.ishafoundation.archives.transcript.model.MTagImpl;
		import mx.utils.StringUtil;
		import name.carter.mark.flex.util.icon.IconUtils;
		import mx.managers.PopUpManager;
		import org.ishafoundation.archives.transcript.model.ReferenceManager;
		import name.carter.mark.flex.util.collection.HashSet;
		import name.carter.mark.flex.util.collection.ISet;
		import mx.collections.ArrayCollection;
		
		public static const CATEGORY_EDIT_EVENT:String = "CATEGORY_EDIT_EVENT";
		
		[Bindable]
		public var referenceMgr:ReferenceManager;
		public var selectedCategoryId:String;
		
		[Bindable]
		public var allowRemove:Boolean = true;
				
		private var alreadySearched:Boolean = false;	
		public var initialCategoryId:String = null;
    	
		private function init():void {
			if (initialCategoryId != null) {
				performAutoSearch(initialCategoryId);
			}
		}

    	private function selectionChanged():void {
    		this.selectedCategoryId = resultsList.selectedItem as String;
    	}
    	
    	internal function searchClicked():void {
    		var searchTermArray:Array = searchTextInput.text.split(" ");
    		var searchTerms:ISet = new HashSet();
    		for each (var searchTerm:String in searchTermArray) {
    			var s:String = StringUtil.trim(searchTerm);
    			if (s.length > 0) {
    				searchTerms.add(s);
    			}
    		}
    		var markupTypeId:String = typeComboBox.selectedItem.toString();
    		if (markupTypeId != "all") {
	    		searchTerms.add(new MTagImpl("markupType", markupTypeId));
	    	}
	    	var results:Array;
	    	if (searchTerms.size() == 0) {
	    		trace("Getting all markup categories"); 
	    		results = this.referenceMgr.getAllCategories();
	    	}
	    	else {
	    		trace("Using search terms: " + searchTerms);
				results = this.referenceMgr.searchCategories(searchTerms);
	    	}
			resultsList.dataProvider = new ArrayCollection(results);
    	}

		private function dispatchCategoryEditEvent():void {
			dispatchEvent(new Event(CATEGORY_EDIT_EVENT));
		}

		private function editClicked(newCategory:Boolean):void {
			var categoryEditDialog:CategoryEditDialog = CategoryEditDialog.createInstance(this.referenceMgr);
            PopUpManager.addPopUp(categoryEditDialog, this, true);
           	var categoryId:String = newCategory ? null : resultsList.selectedItem as String;
            categoryEditDialog.title = newCategory ? "Create new category" : "Edit category";
            if (newCategory) {
            	var initialTypes:Array = [];
            	if (typeComboBox.selectedIndex > 0) {
            		initialTypes.push(typeComboBox.selectedItem as String);
            	}
            	categoryEditDialog.typeIds = new ArrayCollection(initialTypes);
            }
            else {
            	categoryEditDialog.nameTextInput.text = this.referenceMgr.getCategoryName(categoryId);
            	categoryEditDialog.typeIds = new ArrayCollection(this.referenceMgr.getCategoryTypeIdsForCategoryId(categoryId));
            	categoryEditDialog.concepts = new ArrayCollection(this.referenceMgr.getConceptsForCategoryId(categoryId));
            }
			categoryEditDialog.okButton.addEventListener(MouseEvent.CLICK, function(event:MouseEvent):void {
				if (categoryEditDialog.nameTextInput.text == null) {
					// dialog was cancelled so nothing to do
					return;
				}
				var categoryName:String = Utils.normalizeSpace(categoryEditDialog.nameTextInput.text);
				if (newCategory && !referenceMgr.isNewCategoryNameValid(categoryName)) {
					Alert.show("Category name already in use: " + categoryName);
					return;
				}
				// this is a valid category name, so we can close the dialog and continue updating reference file
				categoryEditDialog.closeMe();
				var typeIds:Array = categoryEditDialog.typeIds.toArray();
				var concepts:Array = categoryEditDialog.concepts.toArray();
				referenceMgr.editCategory(categoryName, typeIds, concepts, categoryId, function(categoryId:String):void {
					if (newCategory) {
						Alert.show("Successfully added new category");
					}
					else {
						Alert.show("Successfully edited category");						
					}

					//User didn't want the search to be rerun for category Id (issue 169)
					//performAutoSearch(categoryId);

					dispatchCategoryEditEvent();
					searchClicked();
				}, function(msg:String):void {
					Alert.show(msg, "Failed");
				});
				if (alreadySearched) {
					searchClicked();
					resultsList.validateDisplayList();
					// try to select this category in the results list
					resultsList.selectedItem = categoryId;
				}
			}); 
		}
		
		private function performAutoSearch(categoryId:String):void {
			resultsList.dataProvider = new ArrayCollection([categoryId]);
			selectedCategoryId = categoryId;
			var markupTypeIds:Array = referenceMgr.getCategoryTypeIdsForCategoryId(categoryId);
			if (markupTypeIds.length > 1) {
				typeComboBox.selectedIndex = 0;
			}
			else {
				typeComboBox.selectedItem = markupTypeIds[0];
			}
			var searchString:String = "";
			for each (var concept:String in referenceMgr.getConceptsForCategoryId(categoryId)) {
				if (searchString.length > 0) {
					searchString += " ";
				}
				searchString += concept;
			}
			searchTextInput.text = searchString;
			searchClicked();
			callLater(function():void {
				resultsList.selectedItem = categoryId;
			});			
		}

		private static function getCategoryTypeIdsForComboBox(referenceMgr:ReferenceManager):ArrayCollection {
			var result:ArrayCollection = new ArrayCollection(referenceMgr.getCategoryTypeIds(true, true));
			result.addItemAt("all", 0);
			return result;
		}
		
		private function comboBoxLabelFunc(markupTypeId:String):String {
			if (markupTypeId == "all") {
				return "All";
			}
			else {
				return this.referenceMgr.getCategoryTypeNameFromId(markupTypeId); 
			}
		}
		
    	private function resultLabelFunc(categoryId:String):String {
    		var result:String = this.referenceMgr.getCategoryName(categoryId);
    		var tags:Array = this.referenceMgr.getConceptsForCategoryId(categoryId);
    		if (tags.length > 0) {
    			result += " [" + tags.join(' ') + "]";
    		}
    		return result;
    	}
    	
		private function getIconFunc():Function {
			return IconUtils.getIconFunctionForDataRenderer(resultsList, function(categoryId:String):String {
				var catTypeId:String = referenceMgr.getCategoryTypeIdForCategoryId(categoryId);
				var iconPath:String = Utils.getIconPath(catTypeId);
				return iconPath;				
			}, 16, 16, Utils.DEFAULT_ICON_CLASS);
		}

		private function removeClicked():void {
			var categoryId:String = resultsList.selectedItem as String;
			referenceMgr.countCategoryInstances(categoryId, function(count:int):void {
				if (count == 0) {
					var messagePopUp:MessagePopUp = createMessagePopup("Removing category: " + categoryId);
					referenceMgr.removeCategory(categoryId, function(returnVal:int):void {
						init();
						searchClicked();
						messagePopUp.enableOkButton(returnVal == 0 ? "Could not find category: " + categoryId : "Removed");
					},
					function(msg:String):void {
						messagePopUp.enableOkButton("Failed to remove category", msg);
					});
				}
				else {
					Alert.show("Cannot remove category because it appears at least once (" + count + " instance(s)) in all transcripts");
				}
			}, function(msg:String):void {
				Alert.show(msg, "Error");
			});
		}
		
		private function createMessagePopup(title:String):MessagePopUp {
			var result:MessagePopUp = MessagePopUp.display(this, title, "Please wait...", 250);
			result.okButton.enabled = false;
			return result;
		}
	]]>
	</mx:Script>
	<mx:VBox width="100%" height="100%">
		<mx:HBox width="100%">
			<mx:ComboBox id="typeComboBox" labelFunction="comboBoxLabelFunc" dataProvider="{getCategoryTypeIdsForComboBox(this.referenceMgr)}"/>
			<generic:ConceptTextInput width="100%" id="searchTextInput" restrict="a-z0-9\- " enter="searchClicked()"/>
			<mx:Button label="Search" id="searchButton" click="searchClicked()" width="80"/>			
		</mx:HBox>
		<mx:HBox width="100%" height="100%">
			<mx:List width="100%" height="100%" id="resultsList" horizontalScrollPolicy="auto" allowMultipleSelection="false" labelFunction="resultLabelFunc" iconFunction="{getIconFunc()}" change="selectionChanged()"/>		
		</mx:HBox>
	</mx:VBox>
	<mx:VBox id="adminVBox" height="100%" horizontalAlign="center" verticalAlign="middle" visible="{referenceMgr.isDbaUser}" includeInLayout="{referenceMgr.isDbaUser}">
		<mx:Button label="New..." width="80" click="editClicked(true)"/>
		<mx:Button label="Edit..." width="80" enabled="{resultsList.selectedIndex &gt;= 0}" click="editClicked(false)"/>
		<mx:Button label="Remove" width="80" enabled="{allowRemove &amp;&amp; resultsList.selectedIndex &gt;= 0}" click="removeClicked()"/>
	</mx:VBox>
</mx:HBox>
