<?xml version="1.0" encoding="utf-8"?>
<mx:List xmlns:mx="http://www.adobe.com/2006/mxml" labelFunction="{itemLabelFunc}">
	<mx:Script>
	<![CDATA[
		import mx.controls.Alert;
		import name.carter.mark.flex.util.XMLUtils;
		import org.ishafoundation.archives.transcript.model.EventMetadata;
		import mx.rpc.http.HTTPService;
		import org.ishafoundation.archives.transcript.model.SessionMetadata;
		import org.ishafoundation.archives.transcript.db.XQueryExecutor;
		
		public var xqueryExecutor:XQueryExecutor;
		/**
		 * must accept an array - which represents all the session props for the specified event
		 */
		public var successHandler:Function;
		public var errorHandler:Function;
		
		/**
		 * Setting this to true will do the following:
		 * 
		 * 1. If there are no sessions then 
		 */
		public var autoSelect:Boolean = false;
		
		private var _eventMetadata:EventMetadata;
		
		public function set eventMetadata(newValue:EventMetadata):void {
			_eventMetadata = newValue;
			refresh(null);
		}
		
		public function get eventMetadata():EventMetadata {
			return _eventMetadata;
		}
		
		public function get selectedSessionMetadata():SessionMetadata {
			return selectedItem as SessionMetadata;
		}
		
		public function refresh(selectedSessionId:String):void {
			xqueryExecutor.executeStoredXQuery("retrieve-session-props.xql", {eventId:eventMetadata.id}, function(returnVal:XML):void {
				var sessionMetadataElements:XMLList = returnVal.*;
				var arr:Array = [];
				var propsToSelect:SessionMetadata;
				for each (var sessionMetadataElement:XML in sessionMetadataElements) {
					var sessionId:String = XMLUtils.getAttributeValue(sessionMetadataElement, "_sessionId");
					if (sessionId == null) {
						Alert.show("Session metadata does not have a corresponding session id");
						continue;
					}
					var props:SessionMetadata = new SessionMetadata(sessionMetadataElement, eventMetadata.id, sessionId);
					arr.push(props);
					if (sessionId == selectedSessionId) {
						propsToSelect = props;
					}
				}
				dataProvider = arr;
				if (propsToSelect != null) {
					callLater(function():void {
						selectedItem = propsToSelect;
					});
				}
				if (successHandler != null) {
					successHandler(arr);
				}
			}, function(msg:String):void {
				if (errorHandler != null) {
					errorHandler(msg);
				} 
			}, HTTPService.RESULT_FORMAT_E4X);
		}
		
		private function itemLabelFunc(sessionMetadata:SessionMetadata):String {
			return sessionMetadata.getFullName(eventMetadata);
		}		
	]]>
	</mx:Script>
</mx:List>
