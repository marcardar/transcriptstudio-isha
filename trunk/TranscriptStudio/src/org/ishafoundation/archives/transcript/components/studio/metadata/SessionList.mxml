<?xml version="1.0" encoding="utf-8"?>
<mx:List xmlns:mx="http://www.adobe.com/2006/mxml" labelFunction="{itemLabelFunc}">
	<mx:Script>
	<![CDATA[
		import mx.controls.Alert;
		import name.carter.mark.flex.util.XMLUtils;
		import org.ishafoundation.archives.transcript.model.EventProperties;
		import mx.rpc.http.HTTPService;
		import org.ishafoundation.archives.transcript.model.SessionProperties;
		import org.ishafoundation.archives.transcript.db.XQueryExecutor;
		
		public var xqueryExecutor:XQueryExecutor;
		/**
		 * must accept an array - which represents all the session props for the specified event
		 */
		public var successHandler:Function;
		public var errorHandler:Function;
		
		/**
		 * Setting this to true will do the following:
		 * 
		 * 1. If there are no sessions then 
		 */
		public var autoSelect:Boolean = false;
		
		private var _eventProps:EventProperties;
		
		public function set eventProps(newValue:EventProperties):void {
			_eventProps = newValue;
			refresh(null);
		}
		
		public function get eventProps():EventProperties {
			return _eventProps;
		}
		
		public function get selectedSessionProps():SessionProperties {
			return selectedItem as SessionProperties;
		}
		
		public function refresh(selectedSessionId:String):void {
			xqueryExecutor.executeStoredXQuery("retrieve-session-props.xql", {eventId:eventProps.id}, function(returnVal:XML):void {
				var sessionPropsElements:XMLList = returnVal.*;
				var arr:Array = [];
				var propsToSelect:SessionProperties;
				for each (var sessionPropsElement:XML in sessionPropsElements) {
					var sessionId:String = XMLUtils.getAttributeValue(sessionPropsElement, "_sessionId");
					if (sessionId == null) {
						Alert.show("Session metadata does not have a corresponding session id");
						continue;
					}
					var props:SessionProperties = new SessionProperties(sessionPropsElement, eventProps.id, sessionId);
					arr.push(props);
					if (sessionId == selectedSessionId) {
						propsToSelect = props;
					}
				}
				dataProvider = arr;
				if (propsToSelect != null) {
					callLater(function():void {
						selectedItem = propsToSelect;
					});
				}
				if (successHandler != null) {
					successHandler(arr);
				}
			}, function(msg:String):void {
				if (errorHandler != null) {
					errorHandler(msg);
				} 
			}, HTTPService.RESULT_FORMAT_E4X);
		}
		
		private function itemLabelFunc(sessionProps:SessionProperties):String {
			return sessionProps.getFullName(eventProps);
		}		
	]]>
	</mx:Script>
</mx:List>
